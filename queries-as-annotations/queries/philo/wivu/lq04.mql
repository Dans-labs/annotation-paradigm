// This query by Hendrik Jan looks for acrostic verses in a chapter.
// We allow for a heading at the beginning of the first verse, as in
// Ps 145.

// ident @(#)lq04.mql	1.1 04/02/24

select all objects where
[chapter
   // The word with the aleph is not the first word of the verse when
   // there is a heading as in Ps 145.
   [verse
      [word surface_consonants ~ '^>']
   ]
   [verse
      [word first surface_consonants ~ '^B']
   ]
   [verse
      [word first surface_consonants ~ '^G']
   ]
   [verse
      [word first surface_consonants ~ '^D']
   ]
   [verse
      [word first surface_consonants ~ '^H']
   ]
   [verse
      [word first surface_consonants ~ '^W']
   ]
   [verse
      [word first surface_consonants ~ '^Z']
   ]
   [verse
      [word first surface_consonants ~ '^X']
   ]
   [verse
      [word first surface_consonants ~ '^V']
   ]
   [verse
      [word first surface_consonants ~ '^J']
   ]
   [verse
      [word first surface_consonants ~ '^K']
   ]
   [verse
      [word first surface_consonants ~ '^L']
   ]
   [verse
      [word first surface_consonants ~ '^M']
   ]
   [verse
      [word first surface_consonants ~ '^N']
   ]
   [verse
      [word first surface_consonants ~ '^S']
   ]
   // The order of < and P may be reversed as in Thr 2-4.
   [verse
      [word first surface_consonants ~ '^[<P]']
   ]
   [verse
      [word first surface_consonants ~ '^[P<]']
   ]
   [verse
      [word first surface_consonants ~ '^Y']
   ]
   [verse
      [word first surface_consonants ~ '^Q']
   ]
   [verse
      [word first surface_consonants ~ '^R']
   ]
   [verse
      [word first surface_consonants ~ '^[CF]']
   ]
   [verse
      [word first surface_consonants ~ '^T']
   ]
]
go
